static def addRepos(RepositoryHandler handler) {
    handler.google()
    handler.jcenter()
}

ext.addRepos = this.&addRepos

static def addAllRepos(RepositoryHandler handler) {
    handler.google()
    handler.jcenter()
}

ext.addAllRepos = this.&addAllRepos

def addProjectDependencies(ScriptHandler handler) {
    handler.dependencies {
        classpath projectDeps.androidGradlePlugin
        classpath projectDeps.kotlin.gradlePlugin
    }
}

ext.addProjectDependencies = this.&addProjectDependencies

def addSpotless(Project project) {
    project.apply plugin: 'com.diffplug.spotless'
    project.spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')
            trimTrailingWhitespace()
            endWithNewline()
            // 允許 import 路徑使用萬用字元
            ktlint("0.39.0").userData(["disabled_rules": "no-wildcard-imports"])
        }
        java {
            target "src/*/java/**/*.java"
            googleJavaFormat("1.7").aosp()
            // 移除沒用到的 Import
            removeUnusedImports()
            // 刪除多餘的空白
            trimTrailingWhitespace()
            importOrder('android', 'androidx', 'com', 'junit', 'net', 'org', 'java', 'javax')
        }
    }
    project.gradle.projectsEvaluated {
        project.preBuild.dependsOn project.spotlessApply
    }
}

ext.addSpotless = this.&addSpotless